plugins {
    id 'com.android.application'
    id 'com.github.ben-manes.versions'
    id 'com.github.triplet.play' version '2.8.0'
}

dependencies {
    implementation 'eu.chainfire:libsuperuser:1.1.0.202004101746'
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'com.google.zxing:android-integration:3.3.0'
    implementation 'com.google.zxing:core:3.4.0'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'org.mindrot:jbcrypt:0.4'
    implementation 'com.google.guava:guava:29.0-android'
    implementation 'com.annimon:stream:1.2.1'
    implementation 'com.android.volley:volley:1.1.1'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.core:core:1.3.0'
    implementation 'androidx.documentfile:documentfile:1.0.1'
    implementation 'androidx.fragment:fragment:1.2.5'
    implementation 'androidx.localbroadcastmanager:localbroadcastmanager:1.0.0'
    implementation "androidx.preference:preference:1.1.1"
    implementation 'com.google.dagger:dagger:2.28.1'
    annotationProcessor "com.google.dagger:dagger-compiler:2.28.1"
    androidTestImplementation 'androidx.test:rules:1.2.0'
    androidTestImplementation 'androidx.annotation:annotation:1.1.0'
}

apply from: 'versions.gradle'
def ourVersionCode = versionMajor * 1000000 + versionMinor * 10000 + versionPatch * 100 + versionWrapper
def ourVersionName = "${versionMajor}.${versionMinor}.${versionPatch}.${versionWrapper}"

android {
    // Changes to these values need to be reflected in `.travis.yml`
    compileSdkVersion 30
    buildToolsVersion '30.0.1'
    buildFeatures.dataBinding = true

    defaultConfig {
        applicationId "com.github.catfriend1.syncthingandroid"
        minSdkVersion 16
        targetSdkVersion 30
        versionCode ourVersionCode
        versionName ourVersionName
        testApplicationId 'com.github.catfriend1.syncthingandroid.test'
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }

    signingConfigs {
        release {
            storeFile = {
                def path = System.getenv("SYNCTHING_RELEASE_STORE_FILE")
                return (path) ? file(path) : null
            }()
            storePassword System.getenv("SIGNING_PASSWORD") ?: ""
            keyAlias System.getenv("SYNCTHING_RELEASE_KEY_ALIAS") ?: ""
            keyPassword System.getenv("SIGNING_PASSWORD") ?: ""
        }
    }

    buildTypes {
        release {
            signingConfig = signingConfigs.release.storeFile ? signingConfigs.release : null
        }
        gplay {
            initWith release
        }
        debug {
            applicationIdSuffix ".debug"
            gradle.buildFinished {
                buildResult -> if (buildResult.failure) {
                    logger.lifecycle("BUILD FAILED")
                }
            }
        }
    }

    playConfigs {
        release {
            serviceAccountCredentials = file(System.getenv("SYNCTHING_RELEASE_PLAY_ACCOUNT_CONFIG_FILE") ?: 'keys.json')
        }
        gplay {
            serviceAccountCredentials = file(System.getenv("SYNCTHING_RELEASE_PLAY_ACCOUNT_CONFIG_FILE") ?: 'keys.json')
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    bundle {
        language {
            enableSplit = false
        }
        density {
            enableSplit = true
        }
        abi {
            enableSplit = true
        }
    }

    lintOptions {
        // Gradle 5.6.1: Lint crashes while running pass "IconMissingDensityFolder".
        disable "IconMissingDensityFolder"
        abortOnError true
    }
}

play {
    serviceAccountCredentials = file(System.getenv("SYNCTHING_RELEASE_PLAY_ACCOUNT_CONFIG_FILE") ?: 'keys.json')
    track = 'beta'
    resolutionStrategy = 'ignore'
    defaultToAppBundles = true
}

tasks.whenTaskAdded { task ->
    if (task.name == 'compileDebugSources') {
        // task.dependsOn lint
        // task.mustRunAfter lint
    }
}

/**
 * Some languages are not supported by Google Play, so we ignore them.
 */
task deleteUnsupportedPlayTranslations(type: Delete) {
    delete 'src/main/play/listings/el-EL/'
    delete 'src/main/play/listings/en/'
    delete 'src/main/play/listings/eu/'
    delete 'src/main/play/listings/nb/'
    delete 'src/main/play/listings/nl_BE/'
    delete 'src/main/play/listings/nl-BE/'
    delete 'src/main/play/listings/nn/'
    delete 'src/main/play/listings/ta/'
}

task postBuildScript(type: Exec) {
    executable = 'python'
    args = ['-u', './postbuild.py']
}
